// SPDX-License-Identifier: MIT
pragma solidity >=0.8.13;

import {IERC1155Errors} from "@openzeppelin/contracts/interfaces/draft-IERC6093.sol";
import {
    IERC1155MetadataURI
} from "@openzeppelin/contracts/token/ERC1155/extensions/IERC1155MetadataURI.sol";
import {IERC1155} from "@openzeppelin/contracts/token/ERC1155/IERC1155.sol";
import {ERC1155Utils} from "@openzeppelin/contracts/token/ERC1155/utils/ERC1155Utils.sol";
import {Arrays} from "@openzeppelin/contracts/utils/Arrays.sol";
import {ERC165} from "@openzeppelin/contracts/utils/introspection/ERC165.sol";
import {IERC165} from "@openzeppelin/contracts/utils/introspection/IERC165.sol";

import {HCAContext} from "../hca/HCAContext.sol";

import {IERC1155Singleton} from "./interfaces/IERC1155Singleton.sol";

/// @notice ERC1155 implementation that supports only a single token per ID. Stores owner information to allow
///         fetching ownership information for a tokenId via `ownerOf`.
/// @author OpenZeppelin (https://github.com/OpenZeppelin/openzeppelin-contracts/blob/v5.0.0/contracts/token/ERC1155/ERC1155.sol)
/// @dev This contract has been modified from the implementation at the above link.
abstract contract ERC1155Singleton is
    HCAContext,
    ERC165,
    IERC1155Singleton,
    IERC1155Errors,
    IERC1155MetadataURI
{
    using Arrays for uint256[];
    using Arrays for address[];

    ////////////////////////////////////////////////////////////////////////
    // Storage
    ////////////////////////////////////////////////////////////////////////

    mapping(uint256 id => address account) private _owners;

    mapping(address account => mapping(address operator => bool)) private _operatorApprovals;

    ////////////////////////////////////////////////////////////////////////
    // Events
    ////////////////////////////////////////////////////////////////////////

    /// @notice An approval for all operator was set.
    /// @param owner The owner of the token.
    /// @param approved The approved address.
    /// @param tokenId The token ID.
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    ////////////////////////////////////////////////////////////////////////
    // Initialization
    ////////////////////////////////////////////////////////////////////////

    /// @inheritdoc IERC165
    function supportsInterface(
        bytes4 interfaceId
    ) public view virtual override(ERC165, IERC165) returns (bool) {
        return
            interfaceId == type(IERC1155).interfaceId ||
            interfaceId == type(IERC1155Singleton).interfaceId ||
            interfaceId == type(IERC1155MetadataURI).interfaceId ||
            super.supportsInterface(interfaceId);
    }

    ////////////////////////////////////////////////////////////////////////
    // Implementation
    ////////////////////////////////////////////////////////////////////////

    /// @notice Sets the approval for all operator.
    /// @param operator The operator to set the approval for.
    /// @param approved The approval status.
    function setApprovalForAll(address operator, bool approved) public virtual {
        _setApprovalForAll(_msgSender(), operator, approved);
    }

    /// @notice Transfers a single token from one address to another.
    /// @param from The address to transfer the token from.
    /// @param to The address to transfer the token to.
    /// @param id The token ID.
    /// @param value The amount of tokens to transfer.
    /// @param data Additional data to pass to the receiver.
    /// @dev `to` cannot be the zero address.
    /// @dev If the caller is not `from`, it must have been approved to spend `from`'s tokens via `setApprovalForAll`.
    /// @dev `from` must have a balance of tokens of type `id` of at least `value` amount.
    /// @dev If `to` refers to a smart contract, it must implement IERC1155Receiver.onERC1155Received and return the
    ///      acceptance magic value.
    function safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 value,
        bytes memory data
    ) public virtual {
        address sender = _msgSender();
        if (from != sender && !isApprovedForAll(from, sender)) {
            revert ERC1155MissingApprovalForAll(sender, from);
        }
        _safeTransferFrom(from, to, id, value, data);
    }

    /// @notice Transfers multiple tokens from one address to another.
    /// @param from The address to transfer the tokens from.
    /// @param to The address to transfer the tokens to.
    /// @param ids The token IDs.
    /// @param values The amounts of tokens to transfer.
    /// @param data Additional data to pass to the receiver.
    /// @dev `ids` and `values` must have the same length.
    /// @dev If `to` refers to a smart contract, it must implement IERC1155Receiver.onERC1155BatchReceived and return the
    ///      acceptance magic value.
    function safeBatchTransferFrom(
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory values,
        bytes memory data
    ) public virtual {
        address sender = _msgSender();
        if (from != sender && !isApprovedForAll(from, sender)) {
            revert ERC1155MissingApprovalForAll(sender, from);
        }
        _safeBatchTransferFrom(from, to, ids, values, data);
    }

    /// @inheritdoc IERC1155Singleton
    function ownerOf(uint256 id) public view virtual returns (address owner) {
        return _owners[id];
    }

    /// @notice Returns the URI for a token.
    /// @param id The token ID.
    /// @return uri The URI for the token.
    function uri(uint256 id) public view virtual returns (string memory uri);

    /// @notice Returns the balance of a token for an account.
    /// @param account The account to get the balance for.
    /// @param id The token ID.
    /// @return balance The balance of the token for the account. This will only ever be 1 or 0.
    function balanceOf(address account, uint256 id) public view virtual returns (uint256) {
        return ownerOf(id) == account ? 1 : 0;
    }

    /// @notice Returns the balances of a batch of tokens for an account.
    /// @param accounts The accounts to get the balances for.
    /// @param ids The token IDs.
    /// @return batchBalances The balances of the tokens for the accounts. These will only ever be 1 or 0.
    /// @dev `accounts` and `ids` must have the same length.
    function balanceOfBatch(
        address[] memory accounts,
        uint256[] memory ids
    ) public view virtual returns (uint256[] memory) {
        if (accounts.length != ids.length) {
            revert ERC1155InvalidArrayLength(ids.length, accounts.length);
        }

        uint256[] memory batchBalances = new uint256[](accounts.length);

        for (uint256 i = 0; i < accounts.length; ++i) {
            batchBalances[i] = balanceOf(accounts.unsafeMemoryAccess(i), ids.unsafeMemoryAccess(i));
        }

        return batchBalances;
    }

    /// @notice Returns the approval for all operator.
    /// @param account The account to get the approval for.
    /// @param operator The operator to get the approval for.
    /// @return approved The approval status.
    function isApprovedForAll(
        address account,
        address operator
    ) public view virtual returns (bool) {
        return _operatorApprovals[account][operator];
    }

    ////////////////////////////////////////////////////////////////////////
    // Internal Functions
    ////////////////////////////////////////////////////////////////////////

    /// @notice Apply token updates for each pair in `ids` and `values`.
    /// @param from Address tokens are moved from. Use `address(0)` for mints.
    /// @param to Address tokens are moved to. Use `address(0)` for burns.
    /// @param ids Token IDs to update.
    /// @param values Amounts for each token ID.
    /// @dev Reverts with `ERC1155InvalidArrayLength` if `ids.length != values.length`.
    /// @dev Reverts with `ERC1155InsufficientBalance` if `from` is not the current owner or `value > 1`.
    /// @dev This function does not perform ERC-1155 receiver acceptance checks.
    /// @dev Emits `TransferSingle` when one token ID is updated, otherwise emits `TransferBatch`.
    function _update(
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory values
    ) internal virtual {
        if (ids.length != values.length) {
            revert ERC1155InvalidArrayLength(ids.length, values.length);
        }

        address operator = _msgSender();

        for (uint256 i = 0; i < ids.length; ++i) {
            uint256 id = ids.unsafeMemoryAccess(i);
            uint256 value = values.unsafeMemoryAccess(i);

            if (value > 0) {
                address owner = _owners[id];
                if (owner != from) {
                    revert ERC1155InsufficientBalance(from, 0, value, id);
                } else if (value > 1) {
                    revert ERC1155InsufficientBalance(from, 1, value, id);
                }
                _owners[id] = to;
            }
        }

        if (ids.length == 1) {
            uint256 id = ids.unsafeMemoryAccess(0);
            uint256 value = values.unsafeMemoryAccess(0);
            emit TransferSingle(operator, from, to, id, value);
        } else {
            emit TransferBatch(operator, from, to, ids, values);
        }
    }

    /// @notice Apply token updates and run ERC-1155 receiver acceptance checks.
    /// @param from Address tokens are moved from. Use `address(0)` for mints.
    /// @param to Address tokens are moved to. Use `address(0)` for burns.
    /// @param ids Token IDs to update.
    /// @param values Amounts for each token ID.
    /// @param data Additional calldata passed to receiver hooks.
    /// @dev Calls `_update` before external receiver callbacks.
    /// @dev If `to` is a contract, this calls `onERC1155Received` or `onERC1155BatchReceived`.
    /// @dev Overriding is discouraged because post-callback state writes can introduce reentrancy bugs.
    function _updateWithAcceptanceCheck(
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory values,
        bytes memory data
    ) internal virtual {
        _update(from, to, ids, values);
        if (to != address(0)) {
            address operator = _msgSender();
            if (ids.length == 1) {
                uint256 id = ids.unsafeMemoryAccess(0);
                uint256 value = values.unsafeMemoryAccess(0);
                ERC1155Utils.checkOnERC1155Received(operator, from, to, id, value, data);
            } else {
                ERC1155Utils.checkOnERC1155BatchReceived(operator, from, to, ids, values, data);
            }
        }
    }

    /// @notice Safely transfer `value` tokens of token ID `id` from `from` to `to`.
    /// @param from Address to transfer from.
    /// @param to Address to transfer to.
    /// @param id Token ID to transfer.
    /// @param value Amount to transfer.
    /// @param data Additional calldata passed to receiver hooks.
    /// @dev Reverts with `ERC1155InvalidSender` if `from` is the zero address.
    /// @dev Reverts with `ERC1155InvalidReceiver` if `to` is the zero address.
    /// @dev If `to` is a contract, it must return the ERC-1155 acceptance magic value.
    /// @dev Emits `TransferSingle`.
    function _safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 value,
        bytes memory data
    ) internal {
        if (to == address(0)) {
            revert ERC1155InvalidReceiver(address(0));
        }
        if (from == address(0)) {
            revert ERC1155InvalidSender(address(0));
        }
        (uint256[] memory ids, uint256[] memory values) = _asSingletonArrays(id, value);
        _updateWithAcceptanceCheck(from, to, ids, values, data);
    }

    /// @notice Safely transfer multiple token IDs from `from` to `to`.
    /// @param from Address to transfer from.
    /// @param to Address to transfer to.
    /// @param ids Token IDs to transfer.
    /// @param values Amounts to transfer for each token ID.
    /// @param data Additional calldata passed to receiver hooks.
    /// @dev Reverts with `ERC1155InvalidSender` if `from` is the zero address.
    /// @dev Reverts with `ERC1155InvalidReceiver` if `to` is the zero address.
    /// @dev Reverts with `ERC1155InvalidArrayLength` if `ids.length != values.length`.
    /// @dev If `to` is a contract, it must return the ERC-1155 acceptance magic value.
    /// @dev Emits `TransferBatch`.
    function _safeBatchTransferFrom(
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory values,
        bytes memory data
    ) internal {
        if (to == address(0)) {
            revert ERC1155InvalidReceiver(address(0));
        }
        if (from == address(0)) {
            revert ERC1155InvalidSender(address(0));
        }
        _updateWithAcceptanceCheck(from, to, ids, values, data);
    }

    /// @notice Mint `value` tokens of token ID `id` to `to`.
    /// @param to Address receiving the minted token.
    /// @param id Token ID to mint.
    /// @param value Amount to mint.
    /// @param data Additional calldata passed to receiver hooks.
    /// @dev Reverts with `ERC1155InvalidReceiver` if `to` is the zero address.
    /// @dev If `to` is a contract, it must return the ERC-1155 acceptance magic value.
    /// @dev Emits `TransferSingle`.
    function _mint(address to, uint256 id, uint256 value, bytes memory data) internal {
        if (to == address(0)) {
            revert ERC1155InvalidReceiver(address(0));
        }
        (uint256[] memory ids, uint256[] memory values) = _asSingletonArrays(id, value);
        _updateWithAcceptanceCheck(address(0), to, ids, values, data);
    }

    /// @notice Mint multiple token IDs to `to`.
    /// @param to Address receiving the minted tokens.
    /// @param ids Token IDs to mint.
    /// @param values Amounts to mint for each token ID.
    /// @param data Additional calldata passed to receiver hooks.
    /// @dev Reverts with `ERC1155InvalidReceiver` if `to` is the zero address.
    /// @dev Reverts with `ERC1155InvalidArrayLength` if `ids.length != values.length`.
    /// @dev If `to` is a contract, it must return the ERC-1155 acceptance magic value.
    /// @dev Emits `TransferBatch`.
    function _mintBatch(
        address to,
        uint256[] memory ids,
        uint256[] memory values,
        bytes memory data
    ) internal {
        if (to == address(0)) {
            revert ERC1155InvalidReceiver(address(0));
        }
        _updateWithAcceptanceCheck(address(0), to, ids, values, data);
    }

    /// @notice Burn `value` tokens of token ID `id` from `from`.
    /// @param from Address to burn from.
    /// @param id Token ID to burn.
    /// @param value Amount to burn.
    /// @dev Reverts with `ERC1155InvalidSender` if `from` is the zero address.
    /// @dev Reverts with `ERC1155InsufficientBalance` if `from` is not current owner or `value > 1`.
    /// @dev Emits `TransferSingle`.
    function _burn(address from, uint256 id, uint256 value) internal {
        if (from == address(0)) {
            revert ERC1155InvalidSender(address(0));
        }
        (uint256[] memory ids, uint256[] memory values) = _asSingletonArrays(id, value);
        _updateWithAcceptanceCheck(from, address(0), ids, values, "");
    }

    /// @notice Burn multiple token IDs from `from`.
    /// @param from Address to burn from.
    /// @param ids Token IDs to burn.
    /// @param values Amounts to burn for each token ID.
    /// @dev Reverts with `ERC1155InvalidSender` if `from` is the zero address.
    /// @dev Reverts with `ERC1155InvalidArrayLength` if `ids.length != values.length`.
    /// @dev Reverts with `ERC1155InsufficientBalance` if `from` is not current owner or `value > 1`.
    /// @dev Emits `TransferBatch`.
    function _burnBatch(address from, uint256[] memory ids, uint256[] memory values) internal {
        if (from == address(0)) {
            revert ERC1155InvalidSender(address(0));
        }
        _updateWithAcceptanceCheck(from, address(0), ids, values, "");
    }

    /// @notice Set or clear approval for `operator` to manage all tokens owned by `owner`.
    /// @param owner Token owner granting or revoking approval.
    /// @param operator Operator receiving approval.
    /// @param approved Approval status to set.
    /// @dev Reverts with `ERC1155InvalidOperator` if `operator` is the zero address.
    /// @dev Emits `ApprovalForAll`.
    function _setApprovalForAll(address owner, address operator, bool approved) internal virtual {
        if (operator == address(0)) {
            revert ERC1155InvalidOperator(address(0));
        }
        _operatorApprovals[owner][operator] = approved;
        emit ApprovalForAll(owner, operator, approved);
    }

    ////////////////////////////////////////////////////////////////////////
    // Private Functions
    ////////////////////////////////////////////////////////////////////////

    /// @dev Creates an array in memory with only one value for each of the elements provided.
    function _asSingletonArrays(
        uint256 element1,
        uint256 element2
    ) private pure returns (uint256[] memory array1, uint256[] memory array2) {
        /// @solidity memory-safe-assembly
        assembly {
            // Load the free memory pointer
            array1 := mload(0x40)
            // Set array length to 1
            mstore(array1, 1)
            // Store the single element at the next word after the length (where content starts)
            mstore(add(array1, 0x20), element1)

            // Repeat for next array locating it right after the first array
            array2 := add(array1, 0x40)
            mstore(array2, 1)
            mstore(add(array2, 0x20), element2)

            // Update the free memory pointer by pointing after the second array
            mstore(0x40, add(array2, 0x40))
        }
    }
}
