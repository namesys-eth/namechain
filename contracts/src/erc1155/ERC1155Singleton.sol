// SPDX-License-Identifier: MIT

/// @notice ERC1155 variant enforcing exactly one owner per token ID.
///
///         Instead of the standard nested balance mapping (`id → address → balance`), uses a flat
///         `id → address` ownership mapping. `balanceOf` returns 1 if the account is the owner,
///         0 otherwise. Transferring value > 1 reverts.
///
///         Used by `PermissionedRegistry` to represent domain name ownership as non-divisible tokens.
///         The registry overrides `ownerOf` to add expiry and version validation on top of raw ownership.
///
///         Inherits `HCAContext` so that `_msgSender()` resolves HCA proxy accounts to their real
///         owners for approval checks and operator tracking.
///
/// @dev Portions from OpenZeppelin Contracts (token/ERC1155/ERC1155.sol)
pragma solidity >=0.8.13;

import {IERC1155Errors} from "@openzeppelin/contracts/interfaces/draft-IERC6093.sol";
import {
    IERC1155MetadataURI
} from "@openzeppelin/contracts/token/ERC1155/extensions/IERC1155MetadataURI.sol";
import {IERC1155} from "@openzeppelin/contracts/token/ERC1155/IERC1155.sol";
import {ERC1155Utils} from "@openzeppelin/contracts/token/ERC1155/utils/ERC1155Utils.sol";
import {Arrays} from "@openzeppelin/contracts/utils/Arrays.sol";
import {ERC165} from "@openzeppelin/contracts/utils/introspection/ERC165.sol";
import {IERC165} from "@openzeppelin/contracts/utils/introspection/IERC165.sol";

import {HCAContext} from "../hca/HCAContext.sol";

import {IERC1155Singleton} from "./interfaces/IERC1155Singleton.sol";

abstract contract ERC1155Singleton is
    HCAContext,
    ERC165,
    IERC1155Singleton,
    IERC1155Errors,
    IERC1155MetadataURI
{
    using Arrays for uint256[];
    using Arrays for address[];

    ////////////////////////////////////////////////////////////////////////
    // Storage
    ////////////////////////////////////////////////////////////////////////

    /// @dev Maps each token ID to its single owner address.
    mapping(uint256 id => address account) private _owners;

    /// @dev Standard ERC1155 operator approval mapping.
    mapping(address account => mapping(address operator => bool)) private _operatorApprovals;

    ////////////////////////////////////////////////////////////////////////
    // Events
    ////////////////////////////////////////////////////////////////////////

    /// @dev Declared for ERC721-like per-token approval signaling but not emitted by this contract.
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    ////////////////////////////////////////////////////////////////////////
    // Initialization
    ////////////////////////////////////////////////////////////////////////

    /// @inheritdoc IERC165
    function supportsInterface(
        bytes4 interfaceId
    ) public view virtual override(ERC165, IERC165) returns (bool) {
        return
            interfaceId == type(IERC1155).interfaceId ||
            interfaceId == type(IERC1155Singleton).interfaceId ||
            interfaceId == type(IERC1155MetadataURI).interfaceId ||
            super.supportsInterface(interfaceId);
    }

    ////////////////////////////////////////////////////////////////////////
    // Implementation
    ////////////////////////////////////////////////////////////////////////

    /// @dev See {IERC1155-setApprovalForAll}.
    function setApprovalForAll(address operator, bool approved) public virtual {
        _setApprovalForAll(_msgSender(), operator, approved);
    }

    /// @dev See {IERC1155-safeTransferFrom}.
    function safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 value,
        bytes memory data
    ) public virtual {
        address sender = _msgSender();
        if (from != sender && !isApprovedForAll(from, sender)) {
            revert ERC1155MissingApprovalForAll(sender, from);
        }
        _safeTransferFrom(from, to, id, value, data);
    }

    /// @dev See {IERC1155-safeBatchTransferFrom}.
    function safeBatchTransferFrom(
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory values,
        bytes memory data
    ) public virtual {
        address sender = _msgSender();
        if (from != sender && !isApprovedForAll(from, sender)) {
            revert ERC1155MissingApprovalForAll(sender, from);
        }
        _safeBatchTransferFrom(from, to, ids, values, data);
    }

    function ownerOf(uint256 id) public view virtual returns (address owner) {
        return _owners[id];
    }

    function uri(uint256 /* id */) public view virtual returns (string memory);

    /// @dev See {IERC1155-balanceOf}.
    function balanceOf(address account, uint256 id) public view virtual returns (uint256) {
        return ownerOf(id) == account ? 1 : 0;
    }

    /// @dev See {IERC1155-balanceOfBatch}.
    ///
    /// Requirements:
    ///
    /// - `accounts` and `ids` must have the same length.
    function balanceOfBatch(
        address[] memory accounts,
        uint256[] memory ids
    ) public view virtual returns (uint256[] memory) {
        if (accounts.length != ids.length) {
            revert ERC1155InvalidArrayLength(ids.length, accounts.length);
        }

        uint256[] memory batchBalances = new uint256[](accounts.length);

        for (uint256 i = 0; i < accounts.length; ++i) {
            batchBalances[i] = balanceOf(accounts.unsafeMemoryAccess(i), ids.unsafeMemoryAccess(i));
        }

        return batchBalances;
    }

    /// @dev See {IERC1155-isApprovedForAll}.
    function isApprovedForAll(
        address account,
        address operator
    ) public view virtual returns (bool) {
        return _operatorApprovals[account][operator];
    }

    ////////////////////////////////////////////////////////////////////////
    // Internal Functions
    ////////////////////////////////////////////////////////////////////////

    /// @dev Transfers a `value` amount of tokens of type `id` from `from` to `to`. Will mint (or burn) if `from`
    /// (or `to`) is the zero address.
    ///
    /// Emits a {TransferSingle} event if the arrays contain one element, and {TransferBatch} otherwise.
    ///
    /// Requirements:
    ///
    /// - If `to` refers to a smart contract, it must implement either {IERC1155Receiver-onERC1155Received}
    ///   or {IERC1155Receiver-onERC1155BatchReceived} and return the acceptance magic value.
    /// - `ids` and `values` must have the same length.
    ///
    /// NOTE: The ERC-1155 acceptance check is not performed in this function. See {_updateWithAcceptanceCheck} instead.
    function _update(
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory values
    ) internal virtual {
        if (ids.length != values.length) {
            revert ERC1155InvalidArrayLength(ids.length, values.length);
        }

        address operator = _msgSender();

        for (uint256 i = 0; i < ids.length; ++i) {
            uint256 id = ids.unsafeMemoryAccess(i);
            uint256 value = values.unsafeMemoryAccess(i);

            if (value > 0) {
                address owner = _owners[id];
                if (owner != from) {
                    revert ERC1155InsufficientBalance(from, 0, value, id);
                } else if (value > 1) {
                    revert ERC1155InsufficientBalance(from, 1, value, id);
                }
                _owners[id] = to;
            }
        }

        if (ids.length == 1) {
            uint256 id = ids.unsafeMemoryAccess(0);
            uint256 value = values.unsafeMemoryAccess(0);
            emit TransferSingle(operator, from, to, id, value);
        } else {
            emit TransferBatch(operator, from, to, ids, values);
        }
    }

    /// @dev Version of {_update} that performs the token acceptance check by calling
    /// {IERC1155Receiver-onERC1155Received} or {IERC1155Receiver-onERC1155BatchReceived} on the receiver address if it
    /// contains code (eg. is a smart contract at the moment of execution).
    ///
    /// IMPORTANT: Overriding this function is discouraged because it poses a reentrancy risk from the receiver. So any
    /// update to the contract state after this function would break the check-effect-interaction pattern. Consider
    /// overriding {_update} instead.
    function _updateWithAcceptanceCheck(
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory values,
        bytes memory data
    ) internal virtual {
        _update(from, to, ids, values);
        if (to != address(0)) {
            address operator = _msgSender();
            if (ids.length == 1) {
                uint256 id = ids.unsafeMemoryAccess(0);
                uint256 value = values.unsafeMemoryAccess(0);
                ERC1155Utils.checkOnERC1155Received(operator, from, to, id, value, data);
            } else {
                ERC1155Utils.checkOnERC1155BatchReceived(operator, from, to, ids, values, data);
            }
        }
    }

    /// @dev Transfers a `value` tokens of token type `id` from `from` to `to`.
    ///
    /// Emits a {TransferSingle} event.
    ///
    /// Requirements:
    ///
    /// - `to` cannot be the zero address.
    /// - `from` must have a balance of tokens of type `id` of at least `value` amount.
    /// - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the
    /// acceptance magic value.
    function _safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 value,
        bytes memory data
    ) internal {
        if (to == address(0)) {
            revert ERC1155InvalidReceiver(address(0));
        }
        if (from == address(0)) {
            revert ERC1155InvalidSender(address(0));
        }
        (uint256[] memory ids, uint256[] memory values) = _asSingletonArrays(id, value);
        _updateWithAcceptanceCheck(from, to, ids, values, data);
    }

    /// @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {_safeTransferFrom}.
    ///
    /// Emits a {TransferBatch} event.
    ///
    /// Requirements:
    ///
    /// - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the
    /// acceptance magic value.
    /// - `ids` and `values` must have the same length.
    function _safeBatchTransferFrom(
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory values,
        bytes memory data
    ) internal {
        if (to == address(0)) {
            revert ERC1155InvalidReceiver(address(0));
        }
        if (from == address(0)) {
            revert ERC1155InvalidSender(address(0));
        }
        _updateWithAcceptanceCheck(from, to, ids, values, data);
    }

    /// @dev Creates a `value` amount of tokens of type `id`, and assigns them to `to`.
    ///
    /// Emits a {TransferSingle} event.
    ///
    /// Requirements:
    ///
    /// - `to` cannot be the zero address.
    /// - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the
    /// acceptance magic value.
    function _mint(address to, uint256 id, uint256 value, bytes memory data) internal {
        if (to == address(0)) {
            revert ERC1155InvalidReceiver(address(0));
        }
        (uint256[] memory ids, uint256[] memory values) = _asSingletonArrays(id, value);
        _updateWithAcceptanceCheck(address(0), to, ids, values, data);
    }

    /// @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {_mint}.
    ///
    /// Emits a {TransferBatch} event.
    ///
    /// Requirements:
    ///
    /// - `ids` and `values` must have the same length.
    /// - `to` cannot be the zero address.
    /// - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the
    /// acceptance magic value.
    function _mintBatch(
        address to,
        uint256[] memory ids,
        uint256[] memory values,
        bytes memory data
    ) internal {
        if (to == address(0)) {
            revert ERC1155InvalidReceiver(address(0));
        }
        _updateWithAcceptanceCheck(address(0), to, ids, values, data);
    }

    /// @dev Destroys a `value` amount of tokens of type `id` from `from`
    ///
    /// Emits a {TransferSingle} event.
    ///
    /// Requirements:
    ///
    /// - `from` cannot be the zero address.
    /// - `from` must have at least `value` amount of tokens of type `id`.
    function _burn(address from, uint256 id, uint256 value) internal {
        if (from == address(0)) {
            revert ERC1155InvalidSender(address(0));
        }
        (uint256[] memory ids, uint256[] memory values) = _asSingletonArrays(id, value);
        _updateWithAcceptanceCheck(from, address(0), ids, values, "");
    }

    /// @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {_burn}.
    ///
    /// Emits a {TransferBatch} event.
    ///
    /// Requirements:
    ///
    /// - `from` cannot be the zero address.
    /// - `from` must have at least `value` amount of tokens of type `id`.
    /// - `ids` and `values` must have the same length.
    function _burnBatch(address from, uint256[] memory ids, uint256[] memory values) internal {
        if (from == address(0)) {
            revert ERC1155InvalidSender(address(0));
        }
        _updateWithAcceptanceCheck(from, address(0), ids, values, "");
    }

    /// @dev Approve `operator` to operate on all of `owner` tokens
    ///
    /// Emits an {ApprovalForAll} event.
    ///
    /// Requirements:
    ///
    /// - `operator` cannot be the zero address.
    function _setApprovalForAll(address owner, address operator, bool approved) internal virtual {
        if (operator == address(0)) {
            revert ERC1155InvalidOperator(address(0));
        }
        _operatorApprovals[owner][operator] = approved;
        emit ApprovalForAll(owner, operator, approved);
    }

    ////////////////////////////////////////////////////////////////////////
    // Private Functions
    ////////////////////////////////////////////////////////////////////////

    /// @dev Gas-optimized assembly helper that creates two length-1 memory arrays without Solidity's
    ///      default zero-initialization overhead. Used to adapt single-token operations (`_mint`,
    ///      `_burn`, `_safeTransferFrom`) to the array-based `_update` function.
    function _asSingletonArrays(
        uint256 element1,
        uint256 element2
    ) private pure returns (uint256[] memory array1, uint256[] memory array2) {
        /// @solidity memory-safe-assembly
        assembly {
            // Load the free memory pointer
            array1 := mload(0x40)
            // Set array length to 1
            mstore(array1, 1)
            // Store the single element at the next word after the length (where content starts)
            mstore(add(array1, 0x20), element1)

            // Repeat for next array locating it right after the first array
            array2 := add(array1, 0x40)
            mstore(array2, 1)
            mstore(add(array2, 0x20), element2)

            // Update the free memory pointer by pointing after the second array
            mstore(0x40, add(array2, 0x40))
        }
    }
}
